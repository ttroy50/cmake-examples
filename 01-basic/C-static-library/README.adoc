= Static Library
:toc:
:toc-placement!:

toc::[]

# Introduction

Shows a hello world example which first creates and links a static library. This is a 
simplified example showing the library and binary in the same folder. Typically
these would be in sub-projects as described in section link:../../02-sub-projects[02-sub-projects]

The files in this tutorial are below:

```
$ tree
.
├── CMakeLists.txt
├── include
│   └── static
│       └── Hello.h
└── src
    ├── Hello.cpp
    └── main.cpp
```

  * link:CMakeLists.txt[] - Contains the CMake commands you wish to run
  * link:include/static/Hello.h[] - The header file to include
  * link:src/Hello.cpp[] - A source file to compile
  * link:src/main.cpp[] - The source file with main


# Concepts

## Adding a Static Library（创建一个静态库）

The +add_library()+ function is used to create a library from some source files.
This is called as follows:

（*使用关键字STATIC-静态，这里的target为hello_library,不必与工程同名，只是声明一个静态库target名,这将创建一个名为libhello_library.a的静态库（在二进制文件目录下）*）

（*使用的源文件目录，默认以CMakeLists.txt所在目录为起点*）

[source,cmake]
----
add_library(hello_library STATIC 
    src/Hello.cpp
)
----

This will be used to create a static library with the name libhello_library.a with
the sources in the +add_library+ call.

[NOTE]
====
As mentioned in the previous example, we pass the source files directly to the
+add_library+ call, as recommended for modern CMake.
====

## Populating Including Directories

In this example, we include directories in the library using the +target_include_directories()+ function with the scope set to +PUBLIC+.

（*这里的target目标，为hello_library,其实是库target名*）

（*为target包含的目录为～/include,所以库的源文件只需#include "static/Hello.h"（这就是target's include directory）,而不需使用绝对路径*）

（*这里的范围属性scope，设置成PUBIC,表示当编译这个target或连接link了这个target的其他target时，将该目录添加到target或连接了该target的其他target的包含目录中，所以这也是为什么连接了库的源文件只需#include "static/Hello.h"*）

[source,cmake]
----
target_include_directories(hello_library
    PUBLIC 
        ${PROJECT_SOURCE_DIR}/include
)
----

This will cause the included directory used in the following places:

* When compiling the library
* When compiling any additional target that links the library.

The meaning of scopes are:

* +PRIVATE+ - the directory is added to this target's include directories（将目录添加到该target的包含目录中）
* +INTERFACE+ - the directory is added to the include directories for any targets that link this library.（该目录将添加到连接此target的任何其他target的包含目录中,不包括本target）
* +PUBLIC+ - As above, it is included in this library and also any targets that link this library.（如上所述，它包括在此target中，也包括连接此target的任何其他target）


[TIP]
====
For public headers it is often a good idea to have your include folder be "namespaced"
with sub-directories. 

The directory passed to +target_include_directories+ will be the root of your 
include directory tree and your C++ files should include the path from there to your header.

For this example you can see that we do it as follows:
[source,cpp]
----
#include "static/Hello.h"
----

Using this method means that there is less chance of header filename clashes when
you use multiple libraries in your project. 
====

## Linking a Library

When creating an executable that will use your library you must tell the compiler
about the library. This can be done using the +target_link_libraries()+ function.

(*创建可执行文件，之传递了一个main.cpp，我们还需建可执行文件与库连接起来，我们使用target_link_libraries函数来告诉编译器*)

（*这里只需将范围属性设置成PRIVATE*）

[source,cmake]
----
add_executable(hello_binary 
    src/main.cpp
)

target_link_libraries( hello_binary
    PRIVATE  
        hello_library
)
----

This tells CMake to link the hello_library against the hello_binary executable
during link time. It will also propagate any include directories with +PUBLIC+ or +INTERFACE+ scope
 from the linked library target.

An example of this being called by the compiler is

```
/usr/bin/c++ CMakeFiles/hello_binary.dir/src/main.cpp.o -o hello_binary -rdynamic libhello_library.a
```


# Building the Example

[source,bash]
----
$ mkdir build

$ cd build

$ cmake ..
-- The C compiler identification is GNU 4.8.4
-- The CXX compiler identification is GNU 4.8.4
-- Check for working C compiler: /usr/bin/cc
-- Check for working C compiler: /usr/bin/cc -- works
-- Detecting C compiler ABI info
-- Detecting C compiler ABI info - done
-- Check for working CXX compiler: /usr/bin/c++
-- Check for working CXX compiler: /usr/bin/c++ -- works
-- Detecting CXX compiler ABI info
-- Detecting CXX compiler ABI info - done
-- Configuring done
-- Generating done
-- Build files have been written to: /home/matrim/workspace/cmake-examples/01-basic/C-static-library/build

$ make
Scanning dependencies of target hello_library
[ 50%] Building CXX object CMakeFiles/hello_library.dir/src/Hello.cpp.o
Linking CXX static library libhello_library.a
[ 50%] Built target hello_library
Scanning dependencies of target hello_binary
[100%] Building CXX object CMakeFiles/hello_binary.dir/src/main.cpp.o
Linking CXX executable hello_binary
[100%] Built target hello_binary

$ ls
CMakeCache.txt  CMakeFiles  cmake_install.cmake  hello_binary  libhello_library.a  Makefile

$ ./hello_binary
Hello Static Library!
----
